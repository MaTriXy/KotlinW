buildscript {
    ext.kotlin_version = '1.0.0-beta-2423'
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'net.sf.proguard:proguard-gradle:5.2.1'
    }
}

repositories {
    jcenter()
}

version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'

//noinspection GroovyUnusedAssignment
sourceCompatibility = 1.5
mainClassName = 'com.cypressworks.kotlinw.MainKt'

jar.manifest.attributes 'Main-Class': mainClassName

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'org.eclipse.aether:aether-api:1.0.2.v20150114'
    compile 'org.eclipse.aether:aether-util:1.0.2.v20150114'
    compile 'org.eclipse.aether:aether-impl:1.0.2.v20150114'
    compile 'org.eclipse.aether:aether-connector-basic:1.0.2.v20150114'
    compile 'org.eclipse.aether:aether-transport-file:1.0.2.v20150114'
    compile 'org.eclipse.aether:aether-transport-http:1.0.2.v20150114'
    compile 'org.eclipse.aether:aether-transport-wagon:1.0.2.v20150114'
    compile 'org.apache.maven:maven-aether-provider:3.3.9'
    compile 'org.apache.maven.wagon:wagon-ssh:1.0'
    compile 'org.slf4j:slf4j-nop:1.7.13'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

//noinspection UnnecessaryQualifiedReference
task proguardJar(type: proguard.gradle.ProGuardTask, dependsOn: jar, group: 'build') {
    injars project.jar.archivePath.getPath()
    injars files(configurations.compile)

    libraryjars files(System.getProperty('sun.boot.class.path').split(File.pathSeparator))

    dontwarn 'javax.**'
    dontwarn 'com.sun.javafx.**'
    dontwarn 'com.jcraft.jzlib.**'
    dontwarn 'org.apache.maven.repository.internal.**'
    dontwarn 'org.eclipse.aether.**'
    dontwarn 'org.codehaus.plexus.**'
    dontwarn 'com.google.inject.**'

    keep 'class com.cypressworks.kotlinw.** {*;}'
    keepclassmembers '''enum * {
        public static **[] values();
        public static ** valueOf(java.lang.String);
    }'''

    dontobfuscate()
    dontoptimize()

    //noinspection GroovyAssignabilityCheck,GrUnresolvedAccess
    outjars new File(libsDir.path, "${project.name}-minified-${project.version}.jar")
}